def failureStage = "Unknown" // ê¸°ë³¸ê°’

pipeline {
    agent any

    tools {
        nodejs 'NodeJS-22'
    }
    
    environment {
        GHCR_CREDS_ID = 'ghcr-creds'
        GITHUB_CREDS_ID = 'github-creds'
        TARGET_SSH_CREDS_ID = 'a5-localhost-jenkins'

        TARGET_DEPLOY_SERVER = 'GEEKOM-A5-Server'

        DISCORD_CREDS_ID = 'discord-webhook-url'

        IMAGE_NAME = 'ghcr.io/paul2021-r/project-protostar-nest'

        TARGET_HOST = credentials('ssh-host-creds')
        TARGET_PORT = credentials('ssh-port-creds')
        TARGET_PROJECT_PATH = '/home/hansol/workspace/project-protostar-nest'
        TARGET_INFRA_PATH = '/home/hansol/workspace/project-protostar-server-configs/main-server'
    }

    parameters {
        choice(name: 'TARGET_COLOR', choices: ['Auto', 'Blue', 'Green', 'Both'], description: 'Select the color to deploy. Auto follows BUILD_NUMBER odd/even logic.')
        choice(name: 'DEPLOY_ACTION', choices: ['Full Deploy', 'Build & Deploy Only', 'Switch Traffic Only'], description: 'Select the scope of the deployment.')
    }

    stages {
    // code pull
        stage('Checkout') {
            steps {
                script {
                    try {
                        echo "1. Get the latest code from GitHub"
                        git credentialsId: GITHUB_CREDS_ID, url: 'https://github.com/paul2021-r/project-protostar-nest.git', branch: 'main'
                    } catch (Exception e) {
                        failureStage = "Checkout Stage"
                        throw e
                    }
                }
            }
        }

        stage('Install Dependencies') {
            steps {
                echo "2. Install Dependencies"
                script {
                    try {
                        dir('app') {
                            sh "pnpm install"
                        }
                    } catch (Exception e) {
                        failureStage = "Install Dependencies Stage"
                        throw e
                    }
                }
            }
        }

        // build image
        stage('Build Image') {
            when {
                expression { params.DEPLOY_ACTION != 'Switch Traffic Only' }
            }
            steps {
                echo "5. Build New Docker Image"
                script {
                    try {
                        def imageTag = "${IMAGE_NAME}:${env.BUILD_NUMBER}" 
                        withCredentials([string(credentialsId: 'prod-database-url', variable: 'PROD_DB_URL')]) {
                            sh """
                                export DATABASE_URL=${PROD_DB_URL}
                                docker build -t ${imageTag} ./app
                            """
                        } 
                    } catch (Exception e) {
                        failureStage = "Build Stage"
                        throw e
                    }
                }
            }
        }

        // push image
        stage('Push to GHCR') {
            when {
                expression { params.DEPLOY_ACTION != 'Switch Traffic Only' }
            }
            steps {
                echo "6. Push the Docker Image to GHCR"
                script {
                    try {
                        def imageTag = "${IMAGE_NAME}:${env.BUILD_NUMBER}"
                        withCredentials([usernamePassword(credentialsId: GHCR_CREDS_ID, usernameVariable: 'USER', passwordVariable: 'TOKEN')]) {
                            sh "echo ${TOKEN} | docker login ghcr.io -u ${USER} --password-stdin"
                            sh "docker push ${imageTag}"
                        }
                    } catch (Exception e) {
                        failureStage = "Push to GHCR Stage"
                        throw e
                    }
                }
            }
        }

        // deploy to server
        stage('Deploy to Server') {
            steps {
                echo "7. Deploy the new image to the ${TARGET_DEPLOY_SERVER} server"
                script {
                    try {
                        // 1. Determine Target Services
                        def deployServices = []
                        if (params.TARGET_COLOR == 'Both') {
                             deployServices = ['nestjs-blue', 'nestjs-green']
                        } else if (params.TARGET_COLOR == "Auto") {
                             deployServices = [(env.BUILD_NUMBER.toInteger() % 2 == 0) ? "nestjs-green" : "nestjs-blue"]
                        } else if (params.TARGET_COLOR == "Blue") {
                             deployServices = ["nestjs-blue"]
                        } else {
                             deployServices = ["nestjs-green"]
                        }

                        echo "Target Services via ${params.TARGET_COLOR}: ${deployServices}"

                        def imageTag = "${IMAGE_NAME}:${env.BUILD_NUMBER}"
                        def newTag = "${env.BUILD_NUMBER}"
                        def earlyTag = "${env.BUILD_NUMBER.toInteger() - 1}"
                        
                        withCredentials([usernamePassword(credentialsId: GHCR_CREDS_ID, usernameVariable: 'GHCR_USER', passwordVariable: 'GHCR_TOKEN'), string(credentialsId: 'prod-database-url', variable: 'PROD_DB_URL')]) {
                            sshagent(credentials: [TARGET_SSH_CREDS_ID]) {
                                // 2. Deployment Script Construction
                                def commands = []

                                // Common Setup
                                commands << "set -ex"
                                
                                // Action: Build & Deploy (or Full)
                                if (params.DEPLOY_ACTION != 'Switch Traffic Only') {
                                    commands << "echo '--- Logging in to GHCR on target server ---'"
                                    commands << "echo '${GHCR_TOKEN}' | docker login ghcr.io -u ${GHCR_USER} --password-stdin"

                                    commands << "echo '--- (1/4) Pull the new image : ${imageTag} ---'"
                                    commands << "docker pull ${imageTag}"

                                    commands << "echo '--- (2/4) Replace Server(s) to new  ---'"
                                    commands << "cd ${TARGET_PROJECT_PATH}"
                                    
                                    commands << "echo '--- Refresh main repository ---'"
                                    commands << "git pull origin main" 
                                    commands << "git checkout main"
                                    
                                    commands << "echo '--- Exporting URL ---'"
                                    commands << "export DATABASE_URL='${PROD_DB_URL}'"

                                    // Loop through services
                                    deployServices.each { service ->
                                        def tagEnvVar = (service == "nestjs-green") ? "TAG_GREEN" : "TAG_BLUE"
                                        commands << "echo '--- Exporting new tag for ${service}: ${tagEnvVar}=${newTag} ---'"
                                        commands << "export ${tagEnvVar}='${newTag}'"
                                    }
                                    
                                    // If 'Single' case, we formerly exported the opposite tag too.
                                    if (params.TARGET_COLOR != 'Both') {
                                        def activeService = deployServices[0]
                                        def tagOppositeEnvVar = (activeService == "nestjs-green") ? "TAG_BLUE" : "TAG_GREEN"
                                        commands << "export ${tagOppositeEnvVar}='${earlyTag}'"
                                    }

                                    deployServices.each { service ->
                                        commands << "docker compose --env-file init.env up -d --no-deps --force-recreate ${service}"
                                    }
                                    
                                    commands << "echo '--- Logging out from GHCR on target server ---'"
                                    commands << "docker logout ghcr.io"
                                }

                                // Action: Switch Traffic (or Full)
                                if (params.DEPLOY_ACTION != 'Build & Deploy Only') {
                                    commands << "echo '--- (3/4) Switch Nginx Configuration to change traffic ---'"
                                    
                                    // Only switch upstream if we are targeting a specific single color
                                    if (params.TARGET_COLOR != 'Both') {
                                        def targetService = deployServices[0]
                                        commands << "cd ${TARGET_INFRA_PATH}"
                                        commands << "sed -i 's|server main-nestjs-.*:3000;|server main-${targetService}:3000;|' ./nginx/nginx_upstream_nestjs.conf"   
                                    } else {
                                        commands << "echo 'Skipping Nginx Upstream Switch because TARGET_COLOR is Both (Preserving current active upstream)'"
                                    }

                                    commands << "echo '--- (4/4) Reload Nginx Configuration ---'"
                                    commands << "docker exec main-protostar-nginx nginx -s reload"
                                }

                                commands << "echo '--- All Deploy Proccess is done ---'"

                                // Execute
                                sh "ssh -o StrictHostKeyChecking=no -p ${TARGET_PORT} ${TARGET_HOST} '${commands.join('\n')}'"
                            }
                        }
                    } catch (Exception e) {
                        failureStage = "Deploy Stage"
                        throw e
                    }
                }
            }
        }
    }
   
    post {
        success {
            echo "Deployment succeeded. Now send a discord notification."
            script {
                withCredentials([string(credentialsId: DISCORD_CREDS_ID, variable: 'DISCORD_URL')]) {
                    def message = "{\"content\": \"ðŸš€ Build Process is successfully finished : **[${env.JOB_NAME}]** - **#${env.BUILD_NUMBER}**\"}"

                    sh "curl -X POST -H 'Content-Type: application/json' --data '${message}' ${DISCORD_URL}"
                }

            }

        }
        failure {
            echo "Deployment failed! Now send a discord notification."

            script {
                withCredentials([string(credentialsId: DISCORD_CREDS_ID, variable: 'DISCORD_URL')]) {
                    def message = "{\"content\": \"ðŸ”¥ Build Process is failed at ${failureStage}. Check Build logs ${env.BUILD_URL} : **[${env.JOB_NAME}]** - **#${env.BUILD_NUMBER}**\"}"

                    sh "curl -X POST -H 'Content-Type: application/json' --data '${message}' ${DISCORD_URL}"
                }    
            }
            
        }
        always {
            echo "Logout from GHCR"
            sh "docker logout ghcr.io"
        }
    }
}